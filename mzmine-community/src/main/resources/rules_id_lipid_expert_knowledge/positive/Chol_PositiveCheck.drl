package rules_id_lipid_expert_knowledge.positive;

import io.github.mzmine.modules.dataprocessing.id_lipidid_expertknowledge.utils.adducts.CommonAdductPositive;
import io.github.mzmine.modules.dataprocessing.id_lipidid_expertknowledge.utils.lipids.FoundLipid;
import io.github.mzmine.modules.dataprocessing.id_lipidid_expertknowledge.utils.adducts.FoundAdduct;
import io.github.mzmine.modules.dataprocessing.id_lipidid_expertknowledge.utils.params.MobilePhases;
import java.util.List;

// Declare the global list
global FoundLipid lipid;
global List mobilePhases;
global String sampleType;


rule "Presence [M+H]+"
when
    $adduct: FoundAdduct(adductName == "[M+H]+")
     eval(sampleType.equalsIgnoreCase("PLASMA"))
then
    lipid.setScore(1);
    lipid.setDescrCorrect("Contains [M+H]+, ");
    lipid.setAppliedPresence(lipid.getAppliedPresence() + 1);
end
rule "No presence [M+NH4]+"
when
    not FoundAdduct(adductName == "[M+NH4]+")
    $phase1 : MobilePhases() from mobilePhases
    eval($phase1 == MobilePhases.NH4)
     eval(sampleType.equalsIgnoreCase("PLASMA"))
then
    lipid.setScore(-1);
    lipid.setDescrIncorrect("Missing [M+NH4]+, ");
    lipid.setAppliedPresence(lipid.getAppliedPresence() + 1);
end

rule "Presence [M+NH4]+"
when
    $adduct: FoundAdduct(adductName == "[M+NH4]+")
    $phase1 : MobilePhases() from mobilePhases
    eval($phase1 == MobilePhases.NH4)
     eval(sampleType.equalsIgnoreCase("PLASMA"))
then
    lipid.setScore(1);
    lipid.setDescrCorrect("Contains [M+NH4]+, ");
    lipid.setAppliedPresence(lipid.getAppliedPresence() + 1);
end

rule "Presence [cholestadiene ion]+"
when
    $adduct: FoundAdduct(adductName == "[C27H44]+")
     eval(sampleType.equalsIgnoreCase("PLASMA"))
then
    lipid.setScore(1);
    lipid.setDescrCorrect("Contains [cholestadiene ion]+, ");
    lipid.setAppliedPresence(lipid.getAppliedPresence() + 1);
end



rule "Intensity [M+NH4]+ > [M+H]+"
when
    $adduct1: FoundAdduct(adductName == "[M+NH4]+")
    $adduct2: FoundAdduct(adductName == "[M+H]+")
    $phase1 : MobilePhases() from mobilePhases
    eval($phase1 == MobilePhases.NH4)

    eval($adduct1.getIntensity() > $adduct2.getIntensity())
     eval(sampleType.equalsIgnoreCase("PLASMA"))
then
    lipid.setScore(2);
    lipid.setDescrCorrect("Intensity OK [M+NH4]+ > [M+H]+, ");
    lipid.setAppliedIntensity(lipid.getAppliedIntensity() + 1);
end

rule "Intensity [M+NH4]+ < [M+H]+"
when
    $adduct1: FoundAdduct(adductName == "[M+NH4]+")
    $adduct2: FoundAdduct(adductName == "[M+H]+")
    $phase1 : MobilePhases() from mobilePhases
    eval($phase1 == MobilePhases.NH4)

    eval($adduct1.getIntensity() < $adduct2.getIntensity())
     eval(sampleType.equalsIgnoreCase("PLASMA"))
then
    lipid.setScore(-2);
    lipid.setDescrIncorrect("Intensity NOT OK [M+NH4]+ < [M+H]+, ");
    lipid.setAppliedIntensity(lipid.getAppliedIntensity() + 1);
end